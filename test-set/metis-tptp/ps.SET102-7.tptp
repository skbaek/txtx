%------------------------------------------------------------------------------
% File     : SET102-7 : TPTP v7.3.0. Bugfixed v2.1.0.
% Domain   : Set Theory
% Problem  : Ordered pair member of ordered pair
% Version  : [Qua92] axioms : Augmented.
% English  :

% Refs     : [Qua92] Quaife (1992), Automated Deduction in von Neumann-Bern
% Source   : [Quaife]
% Names    : OP2.2 [Qua92]

% Status   : Unsatisfiable
% Rating   : 0.18 v7.3.0, 0.17 v7.1.0, 0.08 v7.0.0, 0.40 v6.3.0, 0.27 v6.2.0, 0.10 v6.1.0, 0.21 v6.0.0, 0.10 v5.5.0, 0.35 v5.3.0, 0.39 v5.2.0, 0.31 v5.1.0, 0.24 v5.0.0, 0.29 v4.1.0, 0.46 v4.0.1, 0.45 v3.7.0, 0.30 v3.5.0, 0.36 v3.4.0, 0.33 v3.3.0, 0.29 v3.2.0, 0.23 v3.1.0, 0.27 v2.7.0, 0.17 v2.6.0, 0.11 v2.5.0, 0.27 v2.4.0, 0.25 v2.2.1, 0.33 v2.2.0, 0.00 v2.1.0
% Syntax   : Number of clauses     :  146 (  25 non-Horn;  43 unit;  96 RR)
%            Number of atoms       :  295 (  87 equality)
%            Maximal clause size   :    5 (   2 average)
%            Number of predicates  :   10 (   0 propositional; 1-3 arity)
%            Number of functors    :   42 (  10 constant; 0-3 arity)
%            Number of variables   :  275 (  48 singleton)
%            Maximal term depth    :    6 (   2 average)
% SPC      : CNF_UNS_RFO_SEQ_NHN

% Comments : Preceding lemmas are added.
%          : tptp2X 
% Bugfixes : v2.1.0 - Bugfix in SET004-0.ax.
%------------------------------------------------------------------------------
cnf(subclass_members,axiom,
    ( ~ subclass(X,Y) | ~ member(U,X) | member(U,Y) )).

cnf(not_subclass_members1,axiom,
    ( member(not_subclass_element(X,Y),X) | subclass(X,Y) )).

cnf(not_subclass_members2,axiom,
    ( ~ member(not_subclass_element(X,Y),Y) | subclass(X,Y) )).

cnf(class_elements_are_sets,axiom,
    ( subclass(X,universal_class) )).

cnf(equal_implies_subclass1,axiom,
    ( X != Y | subclass(X,Y) )).

cnf(equal_implies_subclass2,axiom,
    ( X != Y | subclass(Y,X) )).

cnf(subclass_implies_equal,axiom,
    ( ~ subclass(X,Y) | ~ subclass(Y,X) | X = Y )).

cnf(unordered_pair_member,axiom,
    ( ~ member(U,unordered_pair(X,Y)) | U = X | U = Y )).

cnf(unordered_pair2,axiom,
    ( ~ member(X,universal_class) | member(X,unordered_pair(X,Y)) )).

cnf(unordered_pair3,axiom,
    ( ~ member(Y,universal_class) | member(Y,unordered_pair(X,Y)) )).

cnf(unordered_pairs_in_universal,axiom,
    ( member(unordered_pair(X,Y),universal_class) )).

cnf(singleton_set,axiom,
    ( unordered_pair(X,X) = singleton(X) )).

cnf(ordered_pair,axiom,
    ( unordered_pair(singleton(X),unordered_pair(X,singleton(Y))) = ordered_pair(X,Y) )).

cnf(cartesian_product1,axiom,
    ( ~ member(ordered_pair(U,V),cross_product(X,Y)) | member(U,X) )).

cnf(cartesian_product2,axiom,
    ( ~ member(ordered_pair(U,V),cross_product(X,Y)) | member(V,Y) )).

cnf(cartesian_product3,axiom,
    ( ~ member(U,X) | ~ member(V,Y) | member(ordered_pair(U,V),cross_product(X,Y)) )).

cnf(cartesian_product4,axiom,
    ( ~ member(Z,cross_product(X,Y)) | ordered_pair(first(Z),second(Z)) = Z )).

cnf(element_relation1,axiom,
    ( subclass(element_relation,cross_product(universal_class,universal_class)) )).

cnf(element_relation2,axiom,
    ( ~ member(ordered_pair(X,Y),element_relation) | member(X,Y) )).

cnf(element_relation3,axiom,
    ( ~ member(ordered_pair(X,Y),cross_product(universal_class,universal_class)) | ~ member(X,Y) | member(ordered_pair(X,Y),element_relation) )).

cnf(intersection1,axiom,
    ( ~ member(Z,intersection(X,Y)) | member(Z,X) )).

cnf(intersection2,axiom,
    ( ~ member(Z,intersection(X,Y)) | member(Z,Y) )).

cnf(intersection3,axiom,
    ( ~ member(Z,X) | ~ member(Z,Y) | member(Z,intersection(X,Y)) )).

cnf(complement1,axiom,
    ( ~ member(Z,complement(X)) | ~ member(Z,X) )).

cnf(complement2,axiom,
    ( ~ member(Z,universal_class) | member(Z,complement(X)) | member(Z,X) )).

cnf(union,axiom,
    ( complement(intersection(complement(X),complement(Y))) = union(X,Y) )).

cnf(symmetric_difference,axiom,
    ( intersection(complement(intersection(X,Y)),complement(intersection(complement(X),complement(Y)))) = symmetric_difference(X,Y) )).

cnf(restriction1,axiom,
    ( intersection(Xr,cross_product(X,Y)) = restrict(Xr,X,Y) )).

cnf(restriction2,axiom,
    ( intersection(cross_product(X,Y),Xr) = restrict(Xr,X,Y) )).

cnf(domain1,axiom,
    ( restrict(X,singleton(Z),universal_class) != null_class | ~ member(Z,domain_of(X)) )).

cnf(domain2,axiom,
    ( ~ member(Z,universal_class) | restrict(X,singleton(Z),universal_class) = null_class | member(Z,domain_of(X)) )).

cnf(rotate1,axiom,
    ( subclass(rotate(X),cross_product(cross_product(universal_class,universal_class),universal_class)) )).

cnf(rotate2,axiom,
    ( ~ member(ordered_pair(ordered_pair(U,V),W),rotate(X)) | member(ordered_pair(ordered_pair(V,W),U),X) )).

cnf(rotate3,axiom,
    ( ~ member(ordered_pair(ordered_pair(V,W),U),X) | ~ member(ordered_pair(ordered_pair(U,V),W),cross_product(cross_product(universal_class,universal_class),universal_class)) | member(ordered_pair(ordered_pair(U,V),W),rotate(X)) )).

cnf(flip1,axiom,
    ( subclass(flip(X),cross_product(cross_product(universal_class,universal_class),universal_class)) )).

cnf(flip2,axiom,
    ( ~ member(ordered_pair(ordered_pair(U,V),W),flip(X)) | member(ordered_pair(ordered_pair(V,U),W),X) )).

cnf(flip3,axiom,
    ( ~ member(ordered_pair(ordered_pair(V,U),W),X) | ~ member(ordered_pair(ordered_pair(U,V),W),cross_product(cross_product(universal_class,universal_class),universal_class)) | member(ordered_pair(ordered_pair(U,V),W),flip(X)) )).

cnf(inverse,axiom,
    ( domain_of(flip(cross_product(Y,universal_class))) = inverse(Y) )).

cnf(range_of,axiom,
    ( domain_of(inverse(Z)) = range_of(Z) )).

cnf(domain,axiom,
    ( first(not_subclass_element(restrict(Z,X,singleton(Y)),null_class)) = domain(Z,X,Y) )).

cnf(range,axiom,
    ( second(not_subclass_element(restrict(Z,singleton(X),Y),null_class)) = range(Z,X,Y) )).

cnf(image,axiom,
    ( range_of(restrict(Xr,X,universal_class)) = image(Xr,X) )).

cnf(successor,axiom,
    ( union(X,singleton(X)) = successor(X) )).

cnf(successor_relation1,axiom,
    ( subclass(successor_relation,cross_product(universal_class,universal_class)) )).

cnf(successor_relation2,axiom,
    ( ~ member(ordered_pair(X,Y),successor_relation) | successor(X) = Y )).

cnf(successor_relation3,axiom,
    ( successor(X) != Y | ~ member(ordered_pair(X,Y),cross_product(universal_class,universal_class)) | member(ordered_pair(X,Y),successor_relation) )).

cnf(inductive1,axiom,
    ( ~ inductive(X) | member(null_class,X) )).

cnf(inductive2,axiom,
    ( ~ inductive(X) | subclass(image(successor_relation,X),X) )).

cnf(inductive3,axiom,
    ( ~ member(null_class,X) | ~ subclass(image(successor_relation,X),X) | inductive(X) )).

cnf(omega_is_inductive1,axiom,
    ( inductive(omega) )).

cnf(omega_is_inductive2,axiom,
    ( ~ inductive(Y) | subclass(omega,Y) )).

cnf(omega_in_universal,axiom,
    ( member(omega,universal_class) )).

cnf(sum_class_definition,axiom,
    ( domain_of(restrict(element_relation,universal_class,X)) = sum_class(X) )).

cnf(sum_class2,axiom,
    ( ~ member(X,universal_class) | member(sum_class(X),universal_class) )).

cnf(power_class_definition,axiom,
    ( complement(image(element_relation,complement(X))) = power_class(X) )).

cnf(power_class2,axiom,
    ( ~ member(U,universal_class) | member(power_class(U),universal_class) )).

cnf(compose1,axiom,
    ( subclass(compose(Yr,Xr),cross_product(universal_class,universal_class)) )).

cnf(compose2,axiom,
    ( ~ member(ordered_pair(Y,Z),compose(Yr,Xr)) | member(Z,image(Yr,image(Xr,singleton(Y)))) )).

cnf(compose3,axiom,
    ( ~ member(Z,image(Yr,image(Xr,singleton(Y)))) | ~ member(ordered_pair(Y,Z),cross_product(universal_class,universal_class)) | member(ordered_pair(Y,Z),compose(Yr,Xr)) )).

cnf(single_valued_class1,axiom,
    ( ~ single_valued_class(X) | subclass(compose(X,inverse(X)),identity_relation) )).

cnf(single_valued_class2,axiom,
    ( ~ subclass(compose(X,inverse(X)),identity_relation) | single_valued_class(X) )).

cnf(function1,axiom,
    ( ~ function(Xf) | subclass(Xf,cross_product(universal_class,universal_class)) )).

cnf(function2,axiom,
    ( ~ function(Xf) | subclass(compose(Xf,inverse(Xf)),identity_relation) )).

cnf(function3,axiom,
    ( ~ subclass(Xf,cross_product(universal_class,universal_class)) | ~ subclass(compose(Xf,inverse(Xf)),identity_relation) | function(Xf) )).

cnf(replacement,axiom,
    ( ~ function(Xf) | ~ member(X,universal_class) | member(image(Xf,X),universal_class) )).

cnf(regularity1,axiom,
    ( X = null_class | member(regular(X),X) )).

cnf(regularity2,axiom,
    ( X = null_class | intersection(X,regular(X)) = null_class )).

cnf(apply,axiom,
    ( sum_class(image(Xf,singleton(Y))) = apply(Xf,Y) )).

cnf(choice1,axiom,
    ( function(choice) )).

cnf(choice2,axiom,
    ( ~ member(Y,universal_class) | Y = null_class | member(apply(choice,Y),Y) )).

cnf(one_to_one1,axiom,
    ( ~ one_to_one(Xf) | function(Xf) )).

cnf(one_to_one2,axiom,
    ( ~ one_to_one(Xf) | function(inverse(Xf)) )).

cnf(one_to_one3,axiom,
    ( ~ function(inverse(Xf)) | ~ function(Xf) | one_to_one(Xf) )).

cnf(subset_relation,axiom,
    ( intersection(cross_product(universal_class,universal_class),intersection(cross_product(universal_class,universal_class),complement(compose(complement(element_relation),inverse(element_relation))))) = subset_relation )).

cnf(identity_relation,axiom,
    ( intersection(inverse(subset_relation),subset_relation) = identity_relation )).

cnf(diagonalisation,axiom,
    ( complement(domain_of(intersection(Xr,identity_relation))) = diagonalise(Xr) )).

cnf(cantor_class,axiom,
    ( intersection(domain_of(X),diagonalise(compose(inverse(element_relation),X))) = cantor(X) )).

cnf(operation1,axiom,
    ( ~ operation(Xf) | function(Xf) )).

cnf(operation2,axiom,
    ( ~ operation(Xf) | cross_product(domain_of(domain_of(Xf)),domain_of(domain_of(Xf))) = domain_of(Xf) )).

cnf(operation3,axiom,
    ( ~ operation(Xf) | subclass(range_of(Xf),domain_of(domain_of(Xf))) )).

cnf(operation4,axiom,
    ( ~ function(Xf) | cross_product(domain_of(domain_of(Xf)),domain_of(domain_of(Xf))) != domain_of(Xf) | ~ subclass(range_of(Xf),domain_of(domain_of(Xf))) | operation(Xf) )).

cnf(compatible1,axiom,
    ( ~ compatible(Xh,Xf1,Xf2) | function(Xh) )).

cnf(compatible2,axiom,
    ( ~ compatible(Xh,Xf1,Xf2) | domain_of(domain_of(Xf1)) = domain_of(Xh) )).

cnf(compatible3,axiom,
    ( ~ compatible(Xh,Xf1,Xf2) | subclass(range_of(Xh),domain_of(domain_of(Xf2))) )).

cnf(compatible4,axiom,
    ( ~ function(Xh) | domain_of(domain_of(Xf1)) != domain_of(Xh) | ~ subclass(range_of(Xh),domain_of(domain_of(Xf2))) | compatible(Xh,Xf1,Xf2) )).

cnf(homomorphism1,axiom,
    ( ~ homomorphism(Xh,Xf1,Xf2) | operation(Xf1) )).

cnf(homomorphism2,axiom,
    ( ~ homomorphism(Xh,Xf1,Xf2) | operation(Xf2) )).

cnf(homomorphism3,axiom,
    ( ~ homomorphism(Xh,Xf1,Xf2) | compatible(Xh,Xf1,Xf2) )).

cnf(homomorphism4,axiom,
    ( ~ homomorphism(Xh,Xf1,Xf2) | ~ member(ordered_pair(X,Y),domain_of(Xf1)) | apply(Xf2,ordered_pair(apply(Xh,X),apply(Xh,Y))) = apply(Xh,apply(Xf1,ordered_pair(X,Y))) )).

cnf(homomorphism5,axiom,
    ( ~ operation(Xf1) | ~ operation(Xf2) | ~ compatible(Xh,Xf1,Xf2) | member(ordered_pair(not_homomorphism1(Xh,Xf1,Xf2),not_homomorphism2(Xh,Xf1,Xf2)),domain_of(Xf1)) | homomorphism(Xh,Xf1,Xf2) )).

cnf(homomorphism6,axiom,
    ( ~ operation(Xf1) | ~ operation(Xf2) | ~ compatible(Xh,Xf1,Xf2) | apply(Xf2,ordered_pair(apply(Xh,not_homomorphism1(Xh,Xf1,Xf2)),apply(Xh,not_homomorphism2(Xh,Xf1,Xf2)))) != apply(Xh,apply(Xf1,ordered_pair(not_homomorphism1(Xh,Xf1,Xf2),not_homomorphism2(Xh,Xf1,Xf2)))) | homomorphism(Xh,Xf1,Xf2) )).

cnf(corollary_1_to_unordered_pair,axiom,
    ( ~ member(ordered_pair(X,Y),cross_product(U,V)) | member(X,unordered_pair(X,Y)) )).

cnf(corollary_2_to_unordered_pair,axiom,
    ( ~ member(ordered_pair(X,Y),cross_product(U,V)) | member(Y,unordered_pair(X,Y)) )).

cnf(corollary_1_to_cartesian_product,axiom,
    ( ~ member(ordered_pair(U,V),cross_product(X,Y)) | member(U,universal_class) )).

cnf(corollary_2_to_cartesian_product,axiom,
    ( ~ member(ordered_pair(U,V),cross_product(X,Y)) | member(V,universal_class) )).

cnf(subclass_is_reflexive,axiom,
    ( subclass(X,X) )).

cnf(transitivity_of_subclass,axiom,
    ( ~ subclass(X,Y) | ~ subclass(Y,Z) | subclass(X,Z) )).

cnf(equality1,axiom,
    ( X = Y | member(not_subclass_element(X,Y),X) | member(not_subclass_element(Y,X),Y) )).

cnf(equality2,axiom,
    ( ~ member(not_subclass_element(X,Y),Y) | X = Y | member(not_subclass_element(Y,X),Y) )).

cnf(equality3,axiom,
    ( ~ member(not_subclass_element(Y,X),X) | X = Y | member(not_subclass_element(X,Y),X) )).

cnf(equality4,axiom,
    ( ~ member(not_subclass_element(X,Y),Y) | ~ member(not_subclass_element(Y,X),X) | X = Y )).

cnf(special_classes_lemma,axiom,
    ( ~ member(Y,intersection(complement(X),X)) )).

cnf(existence_of_null_class,axiom,
    ( ~ member(Z,null_class) )).

cnf(null_class_is_subclass,axiom,
    ( subclass(null_class,X) )).

cnf(corollary_of_null_class_is_subclass,axiom,
    ( ~ subclass(X,null_class) | X = null_class )).

cnf(null_class_is_unique,axiom,
    ( Z = null_class | member(not_subclass_element(Z,null_class),Z) )).

cnf(null_class_is_a_set,axiom,
    ( member(null_class,universal_class) )).

cnf(commutativity_of_unordered_pair,axiom,
    ( unordered_pair(X,Y) = unordered_pair(Y,X) )).

cnf(singleton_in_unordered_pair1,axiom,
    ( subclass(singleton(X),unordered_pair(X,Y)) )).

cnf(singleton_in_unordered_pair2,axiom,
    ( subclass(singleton(Y),unordered_pair(X,Y)) )).

cnf(unordered_pair_equals_singleton1,axiom,
    ( member(Y,universal_class) | unordered_pair(X,Y) = singleton(X) )).

cnf(unordered_pair_equals_singleton2,axiom,
    ( member(X,universal_class) | unordered_pair(X,Y) = singleton(Y) )).

cnf(null_unordered_pair,axiom,
    ( unordered_pair(X,Y) = null_class | member(X,universal_class) | member(Y,universal_class) )).

cnf(left_cancellation,axiom,
    ( unordered_pair(X,Y) != unordered_pair(X,Z) | ~ member(ordered_pair(Y,Z),cross_product(universal_class,universal_class)) | Y = Z )).

cnf(right_cancellation,axiom,
    ( unordered_pair(X,Z) != unordered_pair(Y,Z) | ~ member(ordered_pair(X,Y),cross_product(universal_class,universal_class)) | X = Y )).

cnf(corollary_to_unordered_pair_axiom1,axiom,
    ( ~ member(X,universal_class) | unordered_pair(X,Y) != null_class )).

cnf(corollary_to_unordered_pair_axiom2,axiom,
    ( ~ member(Y,universal_class) | unordered_pair(X,Y) != null_class )).

cnf(corollary_to_unordered_pair_axiom3,axiom,
    ( ~ member(ordered_pair(X,Y),cross_product(U,V)) | unordered_pair(X,Y) != null_class )).

cnf(unordered_pair_is_subset,axiom,
    ( ~ member(X,Z) | ~ member(Y,Z) | subclass(unordered_pair(X,Y),Z) )).

cnf(singletons_are_sets,axiom,
    ( member(singleton(X),universal_class) )).

cnf(corollary_1_to_singletons_are_sets,axiom,
    ( member(singleton(Y),unordered_pair(X,singleton(Y))) )).

cnf(set_in_its_singleton,axiom,
    ( ~ member(X,universal_class) | member(X,singleton(X)) )).

cnf(corollary_to_set_in_its_singleton,axiom,
    ( ~ member(X,universal_class) | singleton(X) != null_class )).

cnf(null_class_in_its_singleton,axiom,
    ( member(null_class,singleton(null_class)) )).

cnf(only_member_in_singleton,axiom,
    ( ~ member(Y,singleton(X)) | Y = X )).

cnf(singleton_is_null_class,axiom,
    ( member(X,universal_class) | singleton(X) = null_class )).

cnf(singleton_identified_by_element1,axiom,
    ( singleton(X) != singleton(Y) | ~ member(X,universal_class) | X = Y )).

cnf(singleton_identified_by_element2,axiom,
    ( singleton(X) != singleton(Y) | ~ member(Y,universal_class) | X = Y )).

cnf(singleton_in_unordered_pair3,axiom,
    ( unordered_pair(Y,Z) != singleton(X) | ~ member(X,universal_class) | X = Y | X = Z )).

cnf(member_exists1,axiom,
    ( ~ member(Y,universal_class) | member(member_of(singleton(Y)),universal_class) )).

cnf(member_exists2,axiom,
    ( ~ member(Y,universal_class) | singleton(member_of(singleton(Y))) = singleton(Y) )).

cnf(member_exists3,axiom,
    ( member(member_of(X),universal_class) | member_of(X) = X )).

cnf(member_exists4,axiom,
    ( singleton(member_of(X)) = X | member_of(X) = X )).

cnf(member_of_singleton_is_unique,axiom,
    ( ~ member(U,universal_class) | member_of(singleton(U)) = U )).

cnf(member_of_non_singleton_unique1,axiom,
    ( member(member_of1(X),universal_class) | member_of(X) = X )).

cnf(member_of_non_singleton_unique2,axiom,
    ( singleton(member_of1(X)) = X | member_of(X) = X )).

cnf(corollary_2_to_singletons_are_sets,axiom,
    ( singleton(member_of(X)) != X | member(X,universal_class) )).

cnf(property_of_singletons1,axiom,
    ( singleton(member_of(X)) != X | ~ member(Y,X) | member_of(X) = Y )).

cnf(property_of_singletons2,axiom,
    ( ~ member(X,Y) | subclass(singleton(X),Y) )).

cnf(two_subsets_of_singleton,axiom,
    ( ~ subclass(X,singleton(Y)) | X = null_class | singleton(Y) = X )).

cnf(number_of_elements_in_class,axiom,
    ( member(not_subclass_element(intersection(complement(singleton(not_subclass_element(X,null_class))),X),null_class),intersection(complement(singleton(not_subclass_element(X,null_class))),X)) | singleton(not_subclass_element(X,null_class)) = X | X = null_class )).

cnf(corollary_2_to_number_of_elements_in_class,axiom,
    ( member(not_subclass_element(intersection(complement(singleton(not_subclass_element(X,null_class))),X),null_class),X) | singleton(not_subclass_element(X,null_class)) = X | X = null_class )).

cnf(corollary_1_to_number_of_elements_in_class,axiom,
    ( not_subclass_element(intersection(complement(singleton(not_subclass_element(X,null_class))),X),null_class) != not_subclass_element(X,null_class) | singleton(not_subclass_element(X,null_class)) = X | X = null_class )).

cnf(unordered_pairs_and_singletons,axiom,
    ( unordered_pair(X,Y) = union(singleton(X),singleton(Y)) )).

cnf(ordered_pair_is_set,axiom,
    ( member(ordered_pair(X,Y),universal_class) )).

cnf(prove_unordered_pair_member_of_ordered_pair_1,negated_conjecture,
    ( ~ member(unordered_pair(x,singleton(y)),ordered_pair(x,y)) )).
%------------------------------------------------------------------------------
