%------------------------------------------------------------------------------
% File     : GRP001-1 : TPTP v7.3.0. Released v1.0.0.
% Domain   : Group Theory
% Problem  : X^2 = identity => commutativity
% Version  : [MOW76] axioms.
% English  : If the square of every element is the identity, the system
%            is commutative.

% Refs     : [Rob63] Robinson (1963), Theorem Proving on the Computer
%          : [Wos65] Wos (1965), Unpublished Note
%          : [MOW76] McCharen et al. (1976), Problems and Experiments for a
%          : [WM76]  Wilson & Minker (1976), Resolution, Refinements, and S
%          : [Ove90] Overbeek (1990), ATP competition announced at CADE-10
%          : [Ove93] Overbeek (1993), The CADE-11 Competitions: A Personal
%          : [LM93]  Lusk & McCune (1993), Uniform Strategies: The CADE-11
% Source   : [MOW76]
% Names    : - [Rob63]
%          : wos10 [WM76]
%          : G1 [MOW76]
%          : CADE-11 Competition 1 [Ove90]
%          : THEOREM 1 [LM93]
%          : xsquared.ver1.in [ANL]

% Status   : Unsatisfiable
% Rating   : 0.00 v5.5.0, 0.06 v5.4.0, 0.07 v5.3.0, 0.17 v5.2.0, 0.00 v2.0.0
% Syntax   : Number of clauses     :   11 (   0 non-Horn;   8 unit;   5 RR)
%            Number of atoms       :   19 (   1 equality)
%            Maximal clause size   :    4 (   2 average)
%            Number of predicates  :    2 (   0 propositional; 2-3 arity)
%            Number of functors    :    6 (   4 constant; 0-2 arity)
%            Number of variables   :   23 (   0 singleton)
%            Maximal term depth    :    2 (   1 average)
% SPC      : CNF_UNS_RFO_SEQ_HRN

% Comments :
%          : tptp2X 
%------------------------------------------------------------------------------
cnf(left_identity,axiom,
    ( product(identity,X,X) )).

cnf(right_identity,axiom,
    ( product(X,identity,X) )).

cnf(left_inverse,axiom,
    ( product(inverse(X),X,identity) )).

cnf(right_inverse,axiom,
    ( product(X,inverse(X),identity) )).

cnf(total_function1,axiom,
    ( product(X,Y,multiply(X,Y)) )).

cnf(total_function2,axiom,
    ( ~ product(X,Y,Z) | ~ product(X,Y,W) | Z = W )).

cnf(associativity1,axiom,
    ( ~ product(X,Y,U) | ~ product(Y,Z,V) | ~ product(U,Z,W) | product(X,V,W) )).

cnf(associativity2,axiom,
    ( ~ product(X,Y,U) | ~ product(Y,Z,V) | ~ product(X,V,W) | product(U,Z,W) )).

cnf(square_element,hypothesis,
    ( product(X,X,identity) )).

cnf(a_times_b_is_c,negated_conjecture,
    ( product(a,b,c) )).

cnf(prove_b_times_a_is_c,negated_conjecture,
    ( ~ product(b,a,c) )).
%------------------------------------------------------------------------------
