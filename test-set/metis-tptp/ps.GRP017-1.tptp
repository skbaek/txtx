%------------------------------------------------------------------------------
% File     : GRP017-1 : TPTP v7.3.0. Released v1.0.0.
% Domain   : Group Theory
% Problem  : The inverse of each element is unique
% Version  : [MOW76] axioms.
% English  : i.e., if ab=ba=identity and ac=ca=identity then b=c

% Refs     : [MOW76] McCharen et al. (1976), Problems and Experiments for a
% Source   : [ANL]
% Names    : G2 [MOW76]
%          : invers1.ver1.in [ANL]

% Status   : Unsatisfiable
% Rating   : 0.00 v6.0.0, 0.11 v5.5.0, 0.06 v5.4.0, 0.07 v5.3.0, 0.17 v5.2.0, 0.12 v5.1.0, 0.00 v2.0.0
% Syntax   : Number of clauses     :   13 (   0 non-Horn;  10 unit;   8 RR)
%            Number of atoms       :   21 (   2 equality)
%            Maximal clause size   :    4 (   2 average)
%            Number of predicates  :    2 (   0 propositional; 2-3 arity)
%            Number of functors    :    6 (   4 constant; 0-2 arity)
%            Number of variables   :   22 (   0 singleton)
%            Maximal term depth    :    2 (   1 average)
% SPC      : CNF_UNS_RFO_SEQ_HRN

% Comments :
%          : tptp2X 
%------------------------------------------------------------------------------
cnf(left_identity,axiom,
    ( product(identity,X,X) )).

cnf(right_identity,axiom,
    ( product(X,identity,X) )).

cnf(left_inverse,axiom,
    ( product(inverse(X),X,identity) )).

cnf(right_inverse,axiom,
    ( product(X,inverse(X),identity) )).

cnf(total_function1,axiom,
    ( product(X,Y,multiply(X,Y)) )).

cnf(total_function2,axiom,
    ( ~ product(X,Y,Z) | ~ product(X,Y,W) | Z = W )).

cnf(associativity1,axiom,
    ( ~ product(X,Y,U) | ~ product(Y,Z,V) | ~ product(U,Z,W) | product(X,V,W) )).

cnf(associativity2,axiom,
    ( ~ product(X,Y,U) | ~ product(Y,Z,V) | ~ product(X,V,W) | product(U,Z,W) )).

cnf(a_times_b_is_identity,hypothesis,
    ( product(a,b,identity) )).

cnf(b_times_a_is_identity,hypothesis,
    ( product(b,a,identity) )).

cnf(a_times_c_is_identity,hypothesis,
    ( product(a,c,identity) )).

cnf(c_times_a_is_identity,hypothesis,
    ( product(c,a,identity) )).

cnf(prove_b_equals_c,negated_conjecture,
    ( b != c )).
%------------------------------------------------------------------------------
