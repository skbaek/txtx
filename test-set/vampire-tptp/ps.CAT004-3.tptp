%------------------------------------------------------------------------------
% File     : CAT004-3 : TPTP v7.3.0. Released v1.0.0.
% Domain   : Category Theory
% Problem  : X and Y epimorphisms, XY well-defined => XY epimorphism
% Version  : [Sco79] axioms : Reduced > Complete.
% English  : If x and y are epimorphisms and xy is well-defined, then
%            xy is an epimorphism.

% Refs     : [Sco79] Scott (1979), Identity and Existence in Intuitionist L
% Source   : [ANL]
% Names    : p4.ver3.in [ANL]

% Status   : Unsatisfiable
% Rating   : 0.12 v7.3.0, 0.25 v7.1.0, 0.17 v7.0.0, 0.20 v6.4.0, 0.27 v6.3.0, 0.09 v6.2.0, 0.20 v6.1.0, 0.43 v6.0.0, 0.20 v5.5.0, 0.40 v5.3.0, 0.39 v5.2.0, 0.31 v5.1.0, 0.24 v5.0.0, 0.14 v4.1.0, 0.15 v4.0.1, 0.27 v4.0.0, 0.09 v3.7.0, 0.10 v3.5.0, 0.09 v3.4.0, 0.08 v3.3.0, 0.21 v3.2.0, 0.38 v3.1.0, 0.27 v2.7.0, 0.25 v2.6.0, 0.30 v2.5.0, 0.50 v2.4.0, 0.44 v2.2.1, 0.44 v2.2.0, 0.22 v2.1.0, 0.56 v2.0.0
% Syntax   : Number of clauses     :   23 (   2 non-Horn;   7 unit;  18 RR)
%            Number of atoms       :   47 (  23 equality)
%            Maximal clause size   :    4 (   2 average)
%            Number of predicates  :    3 (   0 propositional; 1-2 arity)
%            Number of functors    :    8 (   4 constant; 0-2 arity)
%            Number of variables   :   37 (   4 singleton)
%            Maximal term depth    :    3 (   1 average)
% SPC      : CNF_UNS_RFO_SEQ_NHN

% Comments : Axioms simplified by Art Quaife.
%          : tptp2X 
%------------------------------------------------------------------------------
cnf(equivalence_implies_existence1,axiom,
    ( ~ equivalent(X,Y) | there_exists(X) )).

cnf(equivalence_implies_existence2,axiom,
    ( ~ equivalent(X,Y) | X = Y )).

cnf(existence_and_equality_implies_equivalence1,axiom,
    ( ~ there_exists(X) | X != Y | equivalent(X,Y) )).

cnf(domain_has_elements,axiom,
    ( ~ there_exists(domain(X)) | there_exists(X) )).

cnf(codomain_has_elements,axiom,
    ( ~ there_exists(codomain(X)) | there_exists(X) )).

cnf(composition_implies_domain,axiom,
    ( ~ there_exists(compose(X,Y)) | there_exists(domain(X)) )).

cnf(domain_codomain_composition1,axiom,
    ( ~ there_exists(compose(X,Y)) | domain(X) = codomain(Y) )).

cnf(domain_codomain_composition2,axiom,
    ( ~ there_exists(domain(X)) | domain(X) != codomain(Y) | there_exists(compose(X,Y)) )).

cnf(associativity_of_compose,axiom,
    ( compose(X,compose(Y,Z)) = compose(compose(X,Y),Z) )).

cnf(compose_domain,axiom,
    ( compose(X,domain(X)) = X )).

cnf(compose_codomain,axiom,
    ( compose(codomain(X),X) = X )).

cnf(equivalence_implies_existence3,axiom,
    ( ~ equivalent(X,Y) | there_exists(Y) )).

cnf(existence_and_equality_implies_equivalence2,axiom,
    ( ~ there_exists(X) | ~ there_exists(Y) | X != Y | equivalent(X,Y) )).

cnf(composition_implies_codomain,axiom,
    ( ~ there_exists(compose(X,Y)) | there_exists(codomain(X)) )).

cnf(indiscernibles1,axiom,
    ( there_exists(f1(X,Y)) | X = Y )).

cnf(indiscernibles2,axiom,
    ( X = f1(X,Y) | Y = f1(X,Y) | X = Y )).

cnf(indiscernibles3,axiom,
    ( X != f1(X,Y) | Y != f1(X,Y) | X = Y )).

cnf(assume_ab_exists,hypothesis,
    ( there_exists(compose(a,b)) )).

cnf(cancellation_for_product1,hypothesis,
    ( compose(X,a) != Y | compose(Z,a) != Y | X = Z )).

cnf(cancellation_for_product2,hypothesis,
    ( compose(X,b) != Y | compose(Z,b) != Y | X = Z )).

cnf(assume_h_exists,hypothesis,
    ( there_exists(h) )).

cnf(h_ab_equals_g_ab,hypothesis,
    ( compose(h,compose(a,b)) = compose(g,compose(a,b)) )).

cnf(prove_h_equals_g,negated_conjecture,
    ( h != g )).
%------------------------------------------------------------------------------
