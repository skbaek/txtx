%------------------------------------------------------------------------------
% File     : GEO223+2 : TPTP v7.3.0. Released v3.3.0.
% Domain   : Geometry (Constructive)
% Problem  : Corollary to uniqueness of parallels
% Version  : [vPl95] axioms : Reduced > Especial.
% English  : The parallel to line L through a point A is equal to the line,
%            that is orthogonal to the orthogonal to L through A, and goes
%            through A as well.

% Refs     : [vPl95] von Plato (1995), The Axioms of Constructive Geometry
%          : [Li97]  Li (1997), Replacing the Axioms for Connecting Lines a
%          : [Li98]  Li (1998), A Shorter and Intuitive Axiom to Replace th
%          : [ROK06] Raths et al. (2006), The ILTP Problem Library for Intu
% Source   : [ILTP]
% Names    :

% Status   : Theorem
% Rating   : 0.00 v7.3.0, 0.14 v7.2.0, 0.17 v7.1.0, 0.25 v7.0.0, 0.00 v6.1.0, 0.16 v6.0.0, 0.50 v5.5.0, 0.33 v5.4.0, 0.30 v5.3.0, 0.39 v5.2.0, 0.29 v5.0.0, 0.15 v4.1.0, 0.22 v4.0.1, 0.26 v4.0.0, 0.30 v3.7.0, 0.29 v3.5.0, 0.25 v3.4.0, 0.00 v3.3.0
% Syntax   : Number of formulae    :   21 (   8 unit)
%            Number of atoms       :   56 (   0 equality)
%            Maximal formula depth :    9 (   5 average)
%            Number of connectives :   43 (   8   ~;  16   |;   5   &)
%                                         (   0 <=>;  14  =>;   0  <=;   0 <~>)
%                                         (   0  ~|;   0  ~&)
%            Number of predicates  :    5 (   0 propositional; 2-2 arity)
%            Number of functors    :    4 (   0 constant; 2-2 arity)
%            Number of variables   :   52 (   0 sgn;  52   !;   0   ?)
%            Maximal term depth    :    3 (   1 average)
% SPC      : FOF_THM_RFO_NEQ

% Comments :  Definitions unfolded, hence Especial.
%          : tptp2X 
%------------------------------------------------------------------------------
fof(apart1,axiom,(
    ! [X] : 
      ~ distinct_points(X,X) )).

fof(apart2,axiom,(
    ! [X] : 
      ~ distinct_lines(X,X) )).

fof(apart3,axiom,(
    ! [X] : 
      ~ convergent_lines(X,X) )).

fof(apart4,axiom,(
    ! [X,Y,Z] : 
      ( distinct_points(X,Y)
     => ( distinct_points(X,Z)
        | distinct_points(Y,Z) ) ) )).

fof(apart5,axiom,(
    ! [X,Y,Z] : 
      ( distinct_lines(X,Y)
     => ( distinct_lines(X,Z)
        | distinct_lines(Y,Z) ) ) )).

fof(apart6,axiom,(
    ! [X,Y,Z] : 
      ( convergent_lines(X,Y)
     => ( convergent_lines(X,Z)
        | convergent_lines(Y,Z) ) ) )).

fof(con1,axiom,(
    ! [X,Y,Z] : 
      ( distinct_points(X,Y)
     => ( apart_point_and_line(Z,line_connecting(X,Y))
       => ( distinct_points(Z,X)
          & distinct_points(Z,Y) ) ) ) )).

fof(con2,axiom,(
    ! [X,Y,Z] : 
      ( convergent_lines(X,Y)
     => ( ( apart_point_and_line(Z,X)
          | apart_point_and_line(Z,Y) )
       => distinct_points(Z,intersection_point(X,Y)) ) ) )).

fof(cu1,axiom,(
    ! [X,Y,U,V] : 
      ( ( distinct_points(X,Y)
        & distinct_lines(U,V) )
     => ( apart_point_and_line(X,U)
        | apart_point_and_line(X,V)
        | apart_point_and_line(Y,U)
        | apart_point_and_line(Y,V) ) ) )).

fof(ceq1,axiom,(
    ! [X,Y,Z] : 
      ( apart_point_and_line(X,Y)
     => ( distinct_points(X,Z)
        | apart_point_and_line(Z,Y) ) ) )).

fof(ceq2,axiom,(
    ! [X,Y,Z] : 
      ( apart_point_and_line(X,Y)
     => ( distinct_lines(Y,Z)
        | apart_point_and_line(X,Z) ) ) )).

fof(ceq3,axiom,(
    ! [X,Y] : 
      ( convergent_lines(X,Y)
     => distinct_lines(X,Y) ) )).

fof(cp1,axiom,(
    ! [X,Y] : 
      ~ convergent_lines(parallel_through_point(Y,X),Y) )).

fof(cp2,axiom,(
    ! [X,Y] : 
      ~ apart_point_and_line(X,parallel_through_point(Y,X)) )).

fof(cup1,axiom,(
    ! [X,Y,Z] : 
      ( distinct_lines(Y,Z)
     => ( apart_point_and_line(X,Y)
        | apart_point_and_line(X,Z)
        | convergent_lines(Y,Z) ) ) )).

fof(occu1,axiom,(
    ! [L,M] : 
      ( convergent_lines(L,M)
      | unorthogonal_lines(L,M) ) )).

fof(oac1,axiom,(
    ! [L,M,N] : 
      ( ( convergent_lines(L,M)
        & unorthogonal_lines(L,M) )
     => ( ( convergent_lines(L,N)
          & unorthogonal_lines(L,N) )
        | ( convergent_lines(M,N)
          & unorthogonal_lines(M,N) ) ) ) )).

fof(ooc1,axiom,(
    ! [A,L] : 
      ~ unorthogonal_lines(orthogonal_through_point(L,A),L) )).

fof(ooc2,axiom,(
    ! [A,L] : 
      ~ apart_point_and_line(A,orthogonal_through_point(L,A)) )).

fof(ouo1,axiom,(
    ! [A,L,M,N] : 
      ( distinct_lines(L,M)
     => ( apart_point_and_line(A,L)
        | apart_point_and_line(A,M)
        | unorthogonal_lines(L,N)
        | unorthogonal_lines(M,N) ) ) )).

fof(con,conjecture,(
    ! [A,L] : 
      ~ distinct_lines(parallel_through_point(L,A),orthogonal_through_point(orthogonal_through_point(L,A),A)) )).
%------------------------------------------------------------------------------
