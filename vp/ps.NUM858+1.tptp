%------------------------------------------------------------------------------
% File     : NUM858+1 : TPTP v7.3.0. Released v4.1.0.
% Domain   : Number Theory
% Problem  : Basic upper bound replace maximum
% Version  : Especial.
% English  : This is an abstraction of a problem to show equivalence of two 
%            given constraint models. More precisely, the task is to prove that
%            the minimal solutions of a certain constraint model are preserved
%            if the applications of the "maximum" function in it are replaced
%            by "upper bounds" only.

% Refs     : [Bau10] Baumgartner (2010), Email to G. Sutcliffe
%          : [BS09]  Baumgartner & Slaney (2009), Constraint Modelling: A C
% Source   : [Bau10]
% Names    :

% Status   : Theorem
% Rating   : 0.33 v7.3.0, 0.38 v7.2.0, 0.34 v7.1.0, 0.35 v7.0.0, 0.30 v6.4.0, 0.42 v6.2.0, 0.40 v6.0.0, 0.39 v5.5.0, 0.56 v5.4.0, 0.61 v5.3.0, 0.63 v5.2.0, 0.50 v5.1.0, 0.52 v5.0.0, 0.58 v4.1.0
% Syntax   : Number of formulae    :   14 (   1 unit)
%            Number of atoms       :   34 (   4 equality)
%            Maximal formula depth :    8 (   5 average)
%            Number of connectives :   22 (   2   ~;   3   |;   5   &)
%                                         (   8 <=>;   4  =>;   0  <=;   0 <~>)
%                                         (   0  ~|;   0  ~&)
%            Number of predicates  :    7 (   0 propositional; 2-3 arity)
%            Number of functors    :    3 (   0 constant; 1-2 arity)
%            Number of variables   :   37 (   0 sgn;  37   !;   0   ?)
%            Maximal term depth    :    2 (   1 average)
% SPC      : FOF_THM_RFO_SEQ

% Comments : 
%          : tptp2X 
%------------------------------------------------------------------------------
fof(lesseq_ref,axiom,(
    ! [X] : lesseq(X,X) )).

fof(lesseq_trans,axiom,(
    ! [X,Y,Z] : 
      ( ( lesseq(X,Y)
        & lesseq(Y,Z) )
     => lesseq(X,Z) ) )).

fof(lesseq_antisymmetric,axiom,(
    ! [X,Y] : 
      ( ( lesseq(X,Y)
        & lesseq(Y,X) )
     => X = Y ) )).

fof(lesseq_total,axiom,(
    ! [X,Y] : 
      ( lesseq(X,Y)
      | lesseq(Y,X) ) )).

fof(sum_monotone_1,axiom,(
    ! [X,Y,Z] : 
      ( lesseq(X,Y)
    <=> lesseq(sum(Z,X),sum(Z,Y)) ) )).

fof(summation_monotone,axiom,(
    ! [X,Y] : 
      ( lesseq(X,Y)
    <=> lesseq(summation(X),summation(Y)) ) )).

fof(max_1,axiom,(
    ! [X,Y] : 
      ( max(X,Y) = X
      | ~ lesseq(Y,X) ) )).

fof(max_2,axiom,(
    ! [X,Y] : 
      ( max(X,Y) = Y
      | ~ lesseq(X,Y) ) )).

fof(ub,axiom,(
    ! [X,Y,Z] : 
      ( ub(X,Y,Z)
    <=> ( lesseq(X,Z)
        & lesseq(Y,Z) ) ) )).

fof(model_max_1,axiom,(
    ! [X,Y,N] : 
      ( model_max(X,Y,N)
    <=> N = max(X,Y) ) )).

fof(model_ub_1,axiom,(
    ! [X,Y,N] : 
      ( model_ub(X,Y,N)
    <=> ub(X,Y,N) ) )).

fof(minsol_model_max,axiom,(
    ! [X,Y,N] : 
      ( minsol_model_max(X,Y,N)
    <=> ( model_max(X,Y,N)
        & ! [Z] : 
            ( model_max(X,Y,Z)
           => lesseq(N,Z) ) ) ) )).

fof(minsol_model_ub,axiom,(
    ! [X,Y,N] : 
      ( minsol_model_ub(X,Y,N)
    <=> ( model_ub(X,Y,N)
        & ! [Z] : 
            ( model_ub(X,Y,Z)
           => lesseq(N,Z) ) ) ) )).

fof(max_is_ub_1,conjecture,(
    ! [X,Y,Z] : 
      ( minsol_model_ub(X,Y,Z)
    <=> minsol_model_max(X,Y,Z) ) )).
%------------------------------------------------------------------------------
