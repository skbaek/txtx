%------------------------------------------------------------------------------
% File     : SWV385+1 : TPTP v7.3.0. Released v3.3.0.
% Domain   : Software Verification
% Problem  : Priority queue checker: lemma_not_min_elem_not_check_induction02
% Version  : [dNP05] axioms.
% English  :

% Refs     : [Pis06] Piskac (2006), Email to Geoff Sutcliffe
%          : [dNP05] de Nivelle & Piskac (2005), Verification of an Off-Lin
% Source   : [Pis06]
% Names    : cpq_l021 [Pis06]

% Status   : Theorem
% Rating   : 0.10 v7.1.0, 0.13 v7.0.0, 0.10 v6.4.0, 0.15 v6.3.0, 0.12 v6.2.0, 0.20 v6.1.0, 0.13 v5.5.0, 0.11 v5.4.0, 0.18 v5.3.0, 0.19 v5.2.0, 0.15 v5.1.0, 0.14 v5.0.0, 0.12 v4.1.0, 0.17 v4.0.0, 0.21 v3.7.0, 0.15 v3.5.0, 0.16 v3.4.0, 0.21 v3.3.0
% Syntax   : Number of formulae    :   44 (  16 unit)
%            Number of atoms       :   91 (  29 equality)
%            Maximal formula depth :    9 (   5 average)
%            Number of connectives :   69 (  22 ~  ;   6  |;  12  &)
%                                         (   7 <=>;  22 =>;   0 <=)
%                                         (   0 <~>;   0 ~|;   0 ~&)
%            Number of predicates  :   11 (   1 propositional; 0-3 arity)
%            Number of functors    :   19 (   3 constant; 0-3 arity)
%            Number of variables   :  126 (   4 singleton; 126 !;   0 ?)
%            Maximal term depth    :    4 (   2 average)
% SPC      : FOF_THM_RFO_SEQ

% Comments :
%          : tptp2X 
%------------------------------------------------------------------------------
fof(transitivity,axiom,(
    ! [U,V,W] : 
      ( ( less_than(U,V)
        & less_than(V,W) )
     => less_than(U,W) ) )).

fof(totality,axiom,(
    ! [U,V] : 
      ( less_than(U,V)
      | less_than(V,U) ) )).

fof(reflexivity,axiom,(
    ! [U] : less_than(U,U) )).

fof(stricly_smaller_definition,axiom,(
    ! [U,V] : 
      ( strictly_less_than(U,V)
    <=> ( less_than(U,V)
        & ~ less_than(V,U) ) ) )).

fof(bottom_smallest,axiom,(
    ! [U] : less_than(bottom,U) )).

fof(ax18,axiom,(
    ~ isnonempty_slb(create_slb) )).

fof(ax19,axiom,(
    ! [U,V,W] : isnonempty_slb(insert_slb(U,pair(V,W))) )).

fof(ax20,axiom,(
    ! [U] : 
      ~ contains_slb(create_slb,U) )).

fof(ax21,axiom,(
    ! [U,V,W,X] : 
      ( contains_slb(insert_slb(U,pair(V,X)),W)
    <=> ( contains_slb(U,W)
        | V = W ) ) )).

fof(ax22,axiom,(
    ! [U,V] : 
      ~ pair_in_list(create_slb,U,V) )).

fof(ax23,axiom,(
    ! [U,V,W,X,Y] : 
      ( pair_in_list(insert_slb(U,pair(V,X)),W,Y)
    <=> ( pair_in_list(U,W,Y)
        | ( V = W
          & X = Y ) ) ) )).

fof(ax24,axiom,(
    ! [U,V,W] : remove_slb(insert_slb(U,pair(V,W)),V) = U )).

fof(ax25,axiom,(
    ! [U,V,W,X] : 
      ( ( V != W
        & contains_slb(U,W) )
     => remove_slb(insert_slb(U,pair(V,X)),W) = insert_slb(remove_slb(U,W),pair(V,X)) ) )).

fof(ax26,axiom,(
    ! [U,V,W] : lookup_slb(insert_slb(U,pair(V,W)),V) = W )).

fof(ax27,axiom,(
    ! [U,V,W,X] : 
      ( ( V != W
        & contains_slb(U,W) )
     => lookup_slb(insert_slb(U,pair(V,X)),W) = lookup_slb(U,W) ) )).

fof(ax28,axiom,(
    ! [U] : update_slb(create_slb,U) = create_slb )).

fof(ax29,axiom,(
    ! [U,V,W,X] : 
      ( strictly_less_than(X,W)
     => update_slb(insert_slb(U,pair(V,X)),W) = insert_slb(update_slb(U,W),pair(V,W)) ) )).

fof(ax30,axiom,(
    ! [U,V,W,X] : 
      ( less_than(W,X)
     => update_slb(insert_slb(U,pair(V,X)),W) = insert_slb(update_slb(U,W),pair(V,X)) ) )).

fof(ax31,axiom,(
    ! [U] : succ_cpq(U,U) )).

fof(ax32,axiom,(
    ! [U,V,W] : 
      ( succ_cpq(U,V)
     => succ_cpq(U,insert_cpq(V,W)) ) )).

fof(ax33,axiom,(
    ! [U,V,W] : 
      ( succ_cpq(U,V)
     => succ_cpq(U,remove_cpq(V,W)) ) )).

fof(ax34,axiom,(
    ! [U,V] : 
      ( succ_cpq(U,V)
     => succ_cpq(U,findmin_cpq_eff(V)) ) )).

fof(ax35,axiom,(
    ! [U,V] : 
      ( succ_cpq(U,V)
     => succ_cpq(U,removemin_cpq_eff(V)) ) )).

fof(ax36,axiom,(
    ! [U,V] : check_cpq(triple(U,create_slb,V)) )).

fof(ax37,axiom,(
    ! [U,V,W,X,Y] : 
      ( less_than(Y,X)
     => ( check_cpq(triple(U,insert_slb(V,pair(X,Y)),W))
      <=> check_cpq(triple(U,V,W)) ) ) )).

fof(ax38,axiom,(
    ! [U,V,W,X,Y] : 
      ( strictly_less_than(X,Y)
     => ( check_cpq(triple(U,insert_slb(V,pair(X,Y)),W))
      <=> $false ) ) )).

fof(ax39,axiom,(
    ! [U,V,W,X] : 
      ( contains_cpq(triple(U,V,W),X)
    <=> contains_slb(V,X) ) )).

fof(ax40,axiom,(
    ! [U,V] : 
      ( ok(triple(U,V,bad))
    <=> $false ) )).

fof(ax41,axiom,(
    ! [U,V,W] : 
      ( ~ ok(triple(U,V,W))
     => W = bad ) )).

fof(ax42,axiom,(
    ! [U,V,W,X] : insert_cpq(triple(U,V,W),X) = triple(insert_pqp(U,X),insert_slb(V,pair(X,bottom)),W) )).

fof(ax43,axiom,(
    ! [U,V,W,X] : 
      ( ~ contains_slb(V,X)
     => remove_cpq(triple(U,V,W),X) = triple(U,V,bad) ) )).

fof(ax44,axiom,(
    ! [U,V,W,X] : 
      ( ( contains_slb(V,X)
        & less_than(lookup_slb(V,X),X) )
     => remove_cpq(triple(U,V,W),X) = triple(remove_pqp(U,X),remove_slb(V,X),W) ) )).

fof(ax45,axiom,(
    ! [U,V,W,X] : 
      ( ( contains_slb(V,X)
        & strictly_less_than(X,lookup_slb(V,X)) )
     => remove_cpq(triple(U,V,W),X) = triple(remove_pqp(U,X),remove_slb(V,X),bad) ) )).

fof(ax46,axiom,(
    ! [U,V] : findmin_cpq_eff(triple(U,create_slb,V)) = triple(U,create_slb,bad) )).

fof(ax47,axiom,(
    ! [U,V,W,X] : 
      ( ( V != create_slb
        & ~ contains_slb(V,findmin_pqp_res(U)) )
     => findmin_cpq_eff(triple(U,V,W)) = triple(U,update_slb(V,findmin_pqp_res(U)),bad) ) )).

fof(ax48,axiom,(
    ! [U,V,W,X] : 
      ( ( V != create_slb
        & contains_slb(V,findmin_pqp_res(U))
        & strictly_less_than(findmin_pqp_res(U),lookup_slb(V,findmin_pqp_res(U))) )
     => findmin_cpq_eff(triple(U,V,W)) = triple(U,update_slb(V,findmin_pqp_res(U)),bad) ) )).

fof(ax49,axiom,(
    ! [U,V,W,X] : 
      ( ( V != create_slb
        & contains_slb(V,findmin_pqp_res(U))
        & less_than(lookup_slb(V,findmin_pqp_res(U)),findmin_pqp_res(U)) )
     => findmin_cpq_eff(triple(U,V,W)) = triple(U,update_slb(V,findmin_pqp_res(U)),W) ) )).

fof(ax50,axiom,(
    ! [U,V] : findmin_cpq_res(triple(U,create_slb,V)) = bottom )).

fof(ax51,axiom,(
    ! [U,V,W,X] : 
      ( V != create_slb
     => findmin_cpq_res(triple(U,V,W)) = findmin_pqp_res(U) ) )).

fof(ax52,axiom,(
    ! [U] : removemin_cpq_eff(U) = remove_cpq(findmin_cpq_eff(U),findmin_cpq_res(U)) )).

fof(ax53,axiom,(
    ! [U] : removemin_cpq_res(U) = findmin_cpq_res(U) )).

fof(l21_li1316,lemma,(
    ! [U,V,W] : 
      ( ~ ok(triple(U,V,W))
     => ~ ok(im_succ_cpq(triple(U,V,W))) ) )).

fof(l21_li2225,lemma,(
    ! [U,V,W] : 
      ( ~ check_cpq(triple(U,V,W))
     => ( ~ check_cpq(im_succ_cpq(triple(U,V,W)))
        | ~ ok(im_succ_cpq(triple(U,V,W))) ) ) )).

fof(l21_co,conjecture,(
    ! [U,V,W] : 
      ( ( ~ check_cpq(triple(U,V,W))
        | ~ ok(triple(U,V,W)) )
     => ( ~ check_cpq(im_succ_cpq(triple(U,V,W)))
        | ~ ok(im_succ_cpq(triple(U,V,W))) ) ) )).
%------------------------------------------------------------------------------
