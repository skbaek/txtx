%------------------------------------------------------------------------------
% File     : KLE079+1 : TPTP v7.3.0. Released v4.0.0.
% Domain   : Kleene Algebra (Domain Semirings)
% Problem  : Domain and antidomain elements are complements
% Version  : [Hoe08] axioms.
% English  :

% Refs     : [DS08]  Desharnais & Struth (2008), Modal Semirings Revisited
%          : [Hoe08] Hoefner (2008), Email to G. Sutcliffe
% Source   : [Hoe08]
% Names    :

% Status   : Theorem
% Rating   : 0.30 v7.3.0, 0.31 v7.2.0, 0.34 v7.1.0, 0.30 v7.0.0, 0.33 v6.4.0, 0.38 v6.2.0, 0.48 v6.1.0, 0.53 v6.0.0, 0.52 v5.5.0, 0.59 v5.4.0, 0.61 v5.3.0, 0.63 v5.2.0, 0.50 v5.1.0, 0.52 v5.0.0, 0.50 v4.1.0, 0.48 v4.0.0
% Syntax   : Number of formulae    :   18 (  16 unit)
%            Number of atoms       :   21 (  20 equality)
%            Maximal formula depth :    5 (   3 average)
%            Number of connectives :    3 (   0   ~;   0   |;   1   &)
%                                         (   1 <=>;   1  =>;   0  <=;   0 <~>)
%                                         (   0  ~|;   0  ~&)
%            Number of predicates  :    2 (   0 propositional; 2-2 arity)
%            Number of functors    :    6 (   2 constant; 0-2 arity)
%            Number of variables   :   30 (   0 sgn;  30   !;   0   ?)
%            Maximal term depth    :    4 (   2 average)
% SPC      : FOF_THM_RFO_SEQ

% Comments : Equational encoding
%          : tptp2X 
%------------------------------------------------------------------------------
fof(additive_commutativity,axiom,(
    ! [A,B] : addition(A,B) = addition(B,A) )).

fof(additive_associativity,axiom,(
    ! [C,B,A] : addition(A,addition(B,C)) = addition(addition(A,B),C) )).

fof(additive_identity,axiom,(
    ! [A] : addition(A,zero) = A )).

fof(additive_idempotence,axiom,(
    ! [A] : addition(A,A) = A )).

fof(multiplicative_associativity,axiom,(
    ! [A,B,C] : multiplication(A,multiplication(B,C)) = multiplication(multiplication(A,B),C) )).

fof(multiplicative_right_identity,axiom,(
    ! [A] : multiplication(A,one) = A )).

fof(multiplicative_left_identity,axiom,(
    ! [A] : multiplication(one,A) = A )).

fof(right_distributivity,axiom,(
    ! [A,B,C] : multiplication(A,addition(B,C)) = addition(multiplication(A,B),multiplication(A,C)) )).

fof(left_distributivity,axiom,(
    ! [A,B,C] : multiplication(addition(A,B),C) = addition(multiplication(A,C),multiplication(B,C)) )).

fof(right_annihilation,axiom,(
    ! [A] : multiplication(A,zero) = zero )).

fof(left_annihilation,axiom,(
    ! [A] : multiplication(zero,A) = zero )).

fof(order,axiom,(
    ! [A,B] : 
      ( leq(A,B)
    <=> addition(A,B) = B ) )).

fof(domain1,axiom,(
    ! [X0] : addition(X0,multiplication(domain(X0),X0)) = multiplication(domain(X0),X0) )).

fof(domain2,axiom,(
    ! [X0,X1] : domain(multiplication(X0,X1)) = domain(multiplication(X0,domain(X1))) )).

fof(domain3,axiom,(
    ! [X0] : addition(domain(X0),one) = one )).

fof(domain4,axiom,(
    domain(zero) = zero )).

fof(domain5,axiom,(
    ! [X0,X1] : domain(addition(X0,X1)) = addition(domain(X0),domain(X1)) )).

fof(goals,conjecture,(
    ! [X0] : 
      ( ! [X1] : 
          ( addition(domain(X1),antidomain(X1)) = one
          & multiplication(domain(X1),antidomain(X1)) = zero )
     => multiplication(antidomain(X0),domain(X0)) = zero ) )).
%------------------------------------------------------------------------------
