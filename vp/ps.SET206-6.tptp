%------------------------------------------------------------------------------
% File     : SET206-6 : TPTP v7.3.0. Bugfixed v2.1.0.
% Domain   : Set Theory
% Problem  : Cross product with null class 2
% Version  : [Qua92] axioms.
% English  :

% Refs     : [BL+86] Boyer et al. (1986), Set Theory in First-Order Logic:
%          : [Qua92] Quaife (1992), Automated Deduction in von Neumann-Bern
% Source   : [Quaife]
% Names    : CP3.2 [Qua92]

% Status   : Unsatisfiable
% Rating   : 0.47 v7.3.0, 0.42 v7.0.0, 0.53 v6.3.0, 0.45 v6.2.0, 0.70 v6.1.0, 0.79 v6.0.0, 0.70 v5.5.0, 0.85 v5.3.0, 0.89 v5.2.0, 0.81 v5.1.0, 0.88 v5.0.0, 0.86 v4.1.0, 0.85 v4.0.1, 0.82 v3.7.0, 0.60 v3.5.0, 0.64 v3.4.0, 0.75 v3.3.0, 0.71 v3.2.0, 0.77 v3.1.0, 0.64 v2.7.0, 0.75 v2.6.0, 0.78 v2.5.0, 0.82 v2.4.0, 0.88 v2.2.1, 0.83 v2.2.0, 0.67 v2.1.0
% Syntax   : Number of clauses     :  113 (   8 non-Horn;  38 unit;  80 RR)
%            Number of atoms       :  219 (  50 equality)
%            Maximal clause size   :    5 (   2 average)
%            Number of predicates  :   11 (   0 propositional; 1-3 arity)
%            Number of functors    :   47 (  13 constant; 0-3 arity)
%            Number of variables   :  214 (  32 singleton)
%            Maximal term depth    :    6 (   2 average)
% SPC      : CNF_UNS_RFO_SEQ_NHN

% Comments : Quaife proves all these problems by augmenting the axioms with
%            all previously proved theorems. With a few exceptions (the
%            problems that correspond to [BL+86] problems), the TPTP has
%            retained the order in which Quaife presents the problems. The
%            user may create an augmented version of this problem by adding
%            all previously proved theorems (the ones that correspond to
%            [BL+86] are easily identified and positioned using Quaife's
%            naming scheme).
%          : Used as a demodulator by Quaife.
%          : tptp2X 
% Bugfixes : v1.0.1 - Bugfix in SET004-1.ax.
%          : v2.1.0 - Bugfix in SET004-0.ax.
%------------------------------------------------------------------------------
cnf(subclass_members,axiom,
    ( ~ subclass(X,Y) | ~ member(U,X) | member(U,Y) )).

cnf(not_subclass_members1,axiom,
    ( member(not_subclass_element(X,Y),X) | subclass(X,Y) )).

cnf(not_subclass_members2,axiom,
    ( ~ member(not_subclass_element(X,Y),Y) | subclass(X,Y) )).

cnf(class_elements_are_sets,axiom,
    ( subclass(X,universal_class) )).

cnf(equal_implies_subclass1,axiom,
    ( X != Y | subclass(X,Y) )).

cnf(equal_implies_subclass2,axiom,
    ( X != Y | subclass(Y,X) )).

cnf(subclass_implies_equal,axiom,
    ( ~ subclass(X,Y) | ~ subclass(Y,X) | X = Y )).

cnf(unordered_pair_member,axiom,
    ( ~ member(U,unordered_pair(X,Y)) | U = X | U = Y )).

cnf(unordered_pair2,axiom,
    ( ~ member(X,universal_class) | member(X,unordered_pair(X,Y)) )).

cnf(unordered_pair3,axiom,
    ( ~ member(Y,universal_class) | member(Y,unordered_pair(X,Y)) )).

cnf(unordered_pairs_in_universal,axiom,
    ( member(unordered_pair(X,Y),universal_class) )).

cnf(singleton_set,axiom,
    ( unordered_pair(X,X) = singleton(X) )).

cnf(ordered_pair,axiom,
    ( unordered_pair(singleton(X),unordered_pair(X,singleton(Y))) = ordered_pair(X,Y) )).

cnf(cartesian_product1,axiom,
    ( ~ member(ordered_pair(U,V),cross_product(X,Y)) | member(U,X) )).

cnf(cartesian_product2,axiom,
    ( ~ member(ordered_pair(U,V),cross_product(X,Y)) | member(V,Y) )).

cnf(cartesian_product3,axiom,
    ( ~ member(U,X) | ~ member(V,Y) | member(ordered_pair(U,V),cross_product(X,Y)) )).

cnf(cartesian_product4,axiom,
    ( ~ member(Z,cross_product(X,Y)) | ordered_pair(first(Z),second(Z)) = Z )).

cnf(element_relation1,axiom,
    ( subclass(element_relation,cross_product(universal_class,universal_class)) )).

cnf(element_relation2,axiom,
    ( ~ member(ordered_pair(X,Y),element_relation) | member(X,Y) )).

cnf(element_relation3,axiom,
    ( ~ member(ordered_pair(X,Y),cross_product(universal_class,universal_class)) | ~ member(X,Y) | member(ordered_pair(X,Y),element_relation) )).

cnf(intersection1,axiom,
    ( ~ member(Z,intersection(X,Y)) | member(Z,X) )).

cnf(intersection2,axiom,
    ( ~ member(Z,intersection(X,Y)) | member(Z,Y) )).

cnf(intersection3,axiom,
    ( ~ member(Z,X) | ~ member(Z,Y) | member(Z,intersection(X,Y)) )).

cnf(complement1,axiom,
    ( ~ member(Z,complement(X)) | ~ member(Z,X) )).

cnf(complement2,axiom,
    ( ~ member(Z,universal_class) | member(Z,complement(X)) | member(Z,X) )).

cnf(union,axiom,
    ( complement(intersection(complement(X),complement(Y))) = union(X,Y) )).

cnf(symmetric_difference,axiom,
    ( intersection(complement(intersection(X,Y)),complement(intersection(complement(X),complement(Y)))) = symmetric_difference(X,Y) )).

cnf(restriction1,axiom,
    ( intersection(Xr,cross_product(X,Y)) = restrict(Xr,X,Y) )).

cnf(restriction2,axiom,
    ( intersection(cross_product(X,Y),Xr) = restrict(Xr,X,Y) )).

cnf(domain1,axiom,
    ( restrict(X,singleton(Z),universal_class) != null_class | ~ member(Z,domain_of(X)) )).

cnf(domain2,axiom,
    ( ~ member(Z,universal_class) | restrict(X,singleton(Z),universal_class) = null_class | member(Z,domain_of(X)) )).

cnf(rotate1,axiom,
    ( subclass(rotate(X),cross_product(cross_product(universal_class,universal_class),universal_class)) )).

cnf(rotate2,axiom,
    ( ~ member(ordered_pair(ordered_pair(U,V),W),rotate(X)) | member(ordered_pair(ordered_pair(V,W),U),X) )).

cnf(rotate3,axiom,
    ( ~ member(ordered_pair(ordered_pair(V,W),U),X) | ~ member(ordered_pair(ordered_pair(U,V),W),cross_product(cross_product(universal_class,universal_class),universal_class)) | member(ordered_pair(ordered_pair(U,V),W),rotate(X)) )).

cnf(flip1,axiom,
    ( subclass(flip(X),cross_product(cross_product(universal_class,universal_class),universal_class)) )).

cnf(flip2,axiom,
    ( ~ member(ordered_pair(ordered_pair(U,V),W),flip(X)) | member(ordered_pair(ordered_pair(V,U),W),X) )).

cnf(flip3,axiom,
    ( ~ member(ordered_pair(ordered_pair(V,U),W),X) | ~ member(ordered_pair(ordered_pair(U,V),W),cross_product(cross_product(universal_class,universal_class),universal_class)) | member(ordered_pair(ordered_pair(U,V),W),flip(X)) )).

cnf(inverse,axiom,
    ( domain_of(flip(cross_product(Y,universal_class))) = inverse(Y) )).

cnf(range_of,axiom,
    ( domain_of(inverse(Z)) = range_of(Z) )).

cnf(domain,axiom,
    ( first(not_subclass_element(restrict(Z,X,singleton(Y)),null_class)) = domain(Z,X,Y) )).

cnf(range,axiom,
    ( second(not_subclass_element(restrict(Z,singleton(X),Y),null_class)) = range(Z,X,Y) )).

cnf(image,axiom,
    ( range_of(restrict(Xr,X,universal_class)) = image(Xr,X) )).

cnf(successor,axiom,
    ( union(X,singleton(X)) = successor(X) )).

cnf(successor_relation1,axiom,
    ( subclass(successor_relation,cross_product(universal_class,universal_class)) )).

cnf(successor_relation2,axiom,
    ( ~ member(ordered_pair(X,Y),successor_relation) | successor(X) = Y )).

cnf(successor_relation3,axiom,
    ( successor(X) != Y | ~ member(ordered_pair(X,Y),cross_product(universal_class,universal_class)) | member(ordered_pair(X,Y),successor_relation) )).

cnf(inductive1,axiom,
    ( ~ inductive(X) | member(null_class,X) )).

cnf(inductive2,axiom,
    ( ~ inductive(X) | subclass(image(successor_relation,X),X) )).

cnf(inductive3,axiom,
    ( ~ member(null_class,X) | ~ subclass(image(successor_relation,X),X) | inductive(X) )).

cnf(omega_is_inductive1,axiom,
    ( inductive(omega) )).

cnf(omega_is_inductive2,axiom,
    ( ~ inductive(Y) | subclass(omega,Y) )).

cnf(omega_in_universal,axiom,
    ( member(omega,universal_class) )).

cnf(sum_class_definition,axiom,
    ( domain_of(restrict(element_relation,universal_class,X)) = sum_class(X) )).

cnf(sum_class2,axiom,
    ( ~ member(X,universal_class) | member(sum_class(X),universal_class) )).

cnf(power_class_definition,axiom,
    ( complement(image(element_relation,complement(X))) = power_class(X) )).

cnf(power_class2,axiom,
    ( ~ member(U,universal_class) | member(power_class(U),universal_class) )).

cnf(compose1,axiom,
    ( subclass(compose(Yr,Xr),cross_product(universal_class,universal_class)) )).

cnf(compose2,axiom,
    ( ~ member(ordered_pair(Y,Z),compose(Yr,Xr)) | member(Z,image(Yr,image(Xr,singleton(Y)))) )).

cnf(compose3,axiom,
    ( ~ member(Z,image(Yr,image(Xr,singleton(Y)))) | ~ member(ordered_pair(Y,Z),cross_product(universal_class,universal_class)) | member(ordered_pair(Y,Z),compose(Yr,Xr)) )).

cnf(single_valued_class1,axiom,
    ( ~ single_valued_class(X) | subclass(compose(X,inverse(X)),identity_relation) )).

cnf(single_valued_class2,axiom,
    ( ~ subclass(compose(X,inverse(X)),identity_relation) | single_valued_class(X) )).

cnf(function1,axiom,
    ( ~ function(Xf) | subclass(Xf,cross_product(universal_class,universal_class)) )).

cnf(function2,axiom,
    ( ~ function(Xf) | subclass(compose(Xf,inverse(Xf)),identity_relation) )).

cnf(function3,axiom,
    ( ~ subclass(Xf,cross_product(universal_class,universal_class)) | ~ subclass(compose(Xf,inverse(Xf)),identity_relation) | function(Xf) )).

cnf(replacement,axiom,
    ( ~ function(Xf) | ~ member(X,universal_class) | member(image(Xf,X),universal_class) )).

cnf(regularity1,axiom,
    ( X = null_class | member(regular(X),X) )).

cnf(regularity2,axiom,
    ( X = null_class | intersection(X,regular(X)) = null_class )).

cnf(apply,axiom,
    ( sum_class(image(Xf,singleton(Y))) = apply(Xf,Y) )).

cnf(choice1,axiom,
    ( function(choice) )).

cnf(choice2,axiom,
    ( ~ member(Y,universal_class) | Y = null_class | member(apply(choice,Y),Y) )).

cnf(one_to_one1,axiom,
    ( ~ one_to_one(Xf) | function(Xf) )).

cnf(one_to_one2,axiom,
    ( ~ one_to_one(Xf) | function(inverse(Xf)) )).

cnf(one_to_one3,axiom,
    ( ~ function(inverse(Xf)) | ~ function(Xf) | one_to_one(Xf) )).

cnf(subset_relation,axiom,
    ( intersection(cross_product(universal_class,universal_class),intersection(cross_product(universal_class,universal_class),complement(compose(complement(element_relation),inverse(element_relation))))) = subset_relation )).

cnf(identity_relation,axiom,
    ( intersection(inverse(subset_relation),subset_relation) = identity_relation )).

cnf(diagonalisation,axiom,
    ( complement(domain_of(intersection(Xr,identity_relation))) = diagonalise(Xr) )).

cnf(cantor_class,axiom,
    ( intersection(domain_of(X),diagonalise(compose(inverse(element_relation),X))) = cantor(X) )).

cnf(operation1,axiom,
    ( ~ operation(Xf) | function(Xf) )).

cnf(operation2,axiom,
    ( ~ operation(Xf) | cross_product(domain_of(domain_of(Xf)),domain_of(domain_of(Xf))) = domain_of(Xf) )).

cnf(operation3,axiom,
    ( ~ operation(Xf) | subclass(range_of(Xf),domain_of(domain_of(Xf))) )).

cnf(operation4,axiom,
    ( ~ function(Xf) | cross_product(domain_of(domain_of(Xf)),domain_of(domain_of(Xf))) != domain_of(Xf) | ~ subclass(range_of(Xf),domain_of(domain_of(Xf))) | operation(Xf) )).

cnf(compatible1,axiom,
    ( ~ compatible(Xh,Xf1,Xf2) | function(Xh) )).

cnf(compatible2,axiom,
    ( ~ compatible(Xh,Xf1,Xf2) | domain_of(domain_of(Xf1)) = domain_of(Xh) )).

cnf(compatible3,axiom,
    ( ~ compatible(Xh,Xf1,Xf2) | subclass(range_of(Xh),domain_of(domain_of(Xf2))) )).

cnf(compatible4,axiom,
    ( ~ function(Xh) | domain_of(domain_of(Xf1)) != domain_of(Xh) | ~ subclass(range_of(Xh),domain_of(domain_of(Xf2))) | compatible(Xh,Xf1,Xf2) )).

cnf(homomorphism1,axiom,
    ( ~ homomorphism(Xh,Xf1,Xf2) | operation(Xf1) )).

cnf(homomorphism2,axiom,
    ( ~ homomorphism(Xh,Xf1,Xf2) | operation(Xf2) )).

cnf(homomorphism3,axiom,
    ( ~ homomorphism(Xh,Xf1,Xf2) | compatible(Xh,Xf1,Xf2) )).

cnf(homomorphism4,axiom,
    ( ~ homomorphism(Xh,Xf1,Xf2) | ~ member(ordered_pair(X,Y),domain_of(Xf1)) | apply(Xf2,ordered_pair(apply(Xh,X),apply(Xh,Y))) = apply(Xh,apply(Xf1,ordered_pair(X,Y))) )).

cnf(homomorphism5,axiom,
    ( ~ operation(Xf1) | ~ operation(Xf2) | ~ compatible(Xh,Xf1,Xf2) | member(ordered_pair(not_homomorphism1(Xh,Xf1,Xf2),not_homomorphism2(Xh,Xf1,Xf2)),domain_of(Xf1)) | homomorphism(Xh,Xf1,Xf2) )).

cnf(homomorphism6,axiom,
    ( ~ operation(Xf1) | ~ operation(Xf2) | ~ compatible(Xh,Xf1,Xf2) | apply(Xf2,ordered_pair(apply(Xh,not_homomorphism1(Xh,Xf1,Xf2)),apply(Xh,not_homomorphism2(Xh,Xf1,Xf2)))) != apply(Xh,apply(Xf1,ordered_pair(not_homomorphism1(Xh,Xf1,Xf2),not_homomorphism2(Xh,Xf1,Xf2)))) | homomorphism(Xh,Xf1,Xf2) )).

cnf(compose_class_definition1,axiom,
    ( subclass(compose_class(X),cross_product(universal_class,universal_class)) )).

cnf(compose_class_definition2,axiom,
    ( ~ member(ordered_pair(Y,Z),compose_class(X)) | compose(X,Y) = Z )).

cnf(compose_class_definition3,axiom,
    ( ~ member(ordered_pair(Y,Z),cross_product(universal_class,universal_class)) | compose(X,Y) != Z | member(ordered_pair(Y,Z),compose_class(X)) )).

cnf(definition_of_composition_function1,axiom,
    ( subclass(composition_function,cross_product(universal_class,cross_product(universal_class,universal_class))) )).

cnf(definition_of_composition_function2,axiom,
    ( ~ member(ordered_pair(X,ordered_pair(Y,Z)),composition_function) | compose(X,Y) = Z )).

cnf(definition_of_composition_function3,axiom,
    ( ~ member(ordered_pair(X,Y),cross_product(universal_class,universal_class)) | member(ordered_pair(X,ordered_pair(Y,compose(X,Y))),composition_function) )).

cnf(definition_of_domain_relation1,axiom,
    ( subclass(domain_relation,cross_product(universal_class,universal_class)) )).

cnf(definition_of_domain_relation2,axiom,
    ( ~ member(ordered_pair(X,Y),domain_relation) | domain_of(X) = Y )).

cnf(definition_of_domain_relation3,axiom,
    ( ~ member(X,universal_class) | member(ordered_pair(X,domain_of(X)),domain_relation) )).

cnf(single_valued_term_defn1,axiom,
    ( first(not_subclass_element(compose(X,inverse(X)),identity_relation)) = single_valued1(X) )).

cnf(single_valued_term_defn2,axiom,
    ( second(not_subclass_element(compose(X,inverse(X)),identity_relation)) = single_valued2(X) )).

cnf(single_valued_term_defn3,axiom,
    ( domain(X,image(inverse(X),singleton(single_valued1(X))),single_valued2(X)) = single_valued3(X) )).

cnf(compose_can_define_singleton,axiom,
    ( intersection(complement(compose(element_relation,complement(identity_relation))),element_relation) = singleton_relation )).

cnf(application_function_defn1,axiom,
    ( subclass(application_function,cross_product(universal_class,cross_product(universal_class,universal_class))) )).

cnf(application_function_defn2,axiom,
    ( ~ member(ordered_pair(X,ordered_pair(Y,Z)),application_function) | member(Y,domain_of(X)) )).

cnf(application_function_defn3,axiom,
    ( ~ member(ordered_pair(X,ordered_pair(Y,Z)),application_function) | apply(X,Y) = Z )).

cnf(application_function_defn4,axiom,
    ( ~ member(ordered_pair(X,ordered_pair(Y,Z)),cross_product(universal_class,cross_product(universal_class,universal_class))) | ~ member(Y,domain_of(X)) | member(ordered_pair(X,ordered_pair(Y,apply(X,Y))),application_function) )).

cnf(maps1,axiom,
    ( ~ maps(Xf,X,Y) | function(Xf) )).

cnf(maps2,axiom,
    ( ~ maps(Xf,X,Y) | domain_of(Xf) = X )).

cnf(maps3,axiom,
    ( ~ maps(Xf,X,Y) | subclass(range_of(Xf),Y) )).

cnf(maps4,axiom,
    ( ~ function(Xf) | ~ subclass(range_of(Xf),Y) | maps(Xf,domain_of(Xf),Y) )).

cnf(prove_cross_product_with_null_class2_1,negated_conjecture,
    ( cross_product(null_class,y) != null_class )).
%------------------------------------------------------------------------------
